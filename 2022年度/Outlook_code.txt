Sub Make_Mail()

    ' Outlook用の定義
    Dim OutlookObj As Outlook.Application
    Set OutlookObj = CreateObject("Outlook.Application")
    
    ' Excel用の定義
    Dim ex As Excel.Application
    Dim wb As Workbook
    Dim ws1 As Worksheet
    Dim ws2 As Worksheet

    If MsgBox("Outlookメールを作成しますか？", vbYesNo + vbQuestion, "確認") = vbYes Then
        
        ' Excelアプリケーションのオブジェクトを設定
        Set ex = CreateObject("Excel.Application")
        
        ' メール本文を入れたExcelファイルを開くかどうか選択
        If MsgBox("Excelファイルを開きますか", vbYesNo + vbQuestion, "確認") = vbYes Then
            ' Excelウインドウを表示．非表示としたい場合はFalseを設定
            ex.Visible = True
        Else
            ex.Visible = False
        End If
        
        
        ' 指定したExcelブックを開き，オブジェクトに設定
        ' パスは環境にあわせて変更してください
        Set wb = ex.Workbooks.Open("C:\Users\bldyr\OneDrive\デスクトップ\自己研鑽用\02_FT勉強会\2022年度\メール自動送付.xlsm")
    
        'Excelブック1シート目をオブジェクトに設定します。
        Set ws1 = wb.worksheets(1)
        Set ws2 = wb.worksheets(2)
        
        ' Outlookメールを作成
        Dim oItem As Outlook.MailItem
        Set oItem = OutlookObj.CreateItem(olMailItem)
        
        ' HTML形式で本文を作成
        oItem.BodyFormat = olFormatHTML
        
        With ws1
            ' メール本文を取得
            strBody = .Range("Body").Value
            
            ' メール本文の置換
            Dim Rep(4) As String
            Rep(0) = .Range("Input_1").Value
            Rep(1) = .Range("Input_2").Value
            Rep(2) = .Range("Input_3").Value
            Rep(3) = .Range("Input_4").Value
            Rep(4) = .Range("Input_5").Value
            
            For i = LBound(Rep) To UBound(Rep)
                ' 1つずつ置換
                strBody = Replace(strBody, "{Input_" & i + 1 & "}", Rep(i))
            Next i

            ' URLを取得
            Dim Link(2) As String
            Link(0) = .Range("URL_1").Value
            Link(1) = .Range("URL_2").Value
            Link(2) = .Range("URL_3").Value
            
            ' メールにURLを挿入
            Dim URL(2) As String
            For j = LBound(Link) To UBound(Link)
                URL(j) = "<a href=""" & Link(j) & """>" & Link(j) & "</a>"
                Debug.Print "j:" & j, URL(j)
                strBody = Replace(strBody, "{URL_" & j + 1 & "}", URL(j))
            Next j
            
        End With
        
        With oItem
            .To = ws2.Range("To").Value   'To宛先
            .CC = ws2.Range("Cc").Value   'cc宛先
            .BCC = ws2.Range("Bcc").Value  'bcc宛先
            .Subject = ws1.Range("Subject").Value     '件名
            .HTMLBody = strBody ' 本文
        End With

        ' ファイルを添付
        For k = 0 To 2#
            If Not ws1.Range("File").Offset(k, 0) = "" Then
                Dim AttachedFile As String
                AttachedFile = ws1.Range("File").Offset(k, 0).Value
                ' ファイルを添付
                oItem.Attachments.Add Source:=AttachedFile
            End If
        Next k

        ' メールの表示
        oItem.Display
        
        ' テキスト形式を変更
        If ws1.Range("Format").Value = "リッチテキスト形式" Then
            oItem.BodyFormat = olFormatRichText 'リッチテキスト形式に変更する
        ElseIf ws1.Range("Format").Value = "テキスト形式" Then
            oItem.BodyFormat = olFormatPlain 'テキスト形式に変更する
        End If
        
        'オブジェクトを解放
        ex.Quit
        
        Set olItem = Nothing
        Set wb = Nothing
        Set ws1 = Nothing
        Set ws2 = Nothing
        
        
        MsgBox "メールの作成が完了しました．内容を確認の上，送付願います．", vbInformation
        
    ' 作成の必要なければ処理を中断
    Else
        MsgBox "処理を中断します．", vbInformation
        
    End If

    
End Sub
